#!/bin/sed -Ef
s/[^][+,.<>!0-9-]//g
s/^/% /
s/$/#$000;/
:a
s/%(.)/\1%/
/%\+/{
/\$255/{
s/\$255/$000/
ba}
/\$.99/{
s/\$099/$100/
s/\$199/$200/
ba}
/\$..9/s/.*\$.(.)9.*/&\1/
/\$..[0-8]/s/.*\$..(.).*/&\1/
s/8$/9/
s/7$/8/
s/6$/7/
s/5$/6/
s/4$/5/
s/3$/4/
s/2$/3/
s/1$/2/
s/0$/1/
/\$..9/{
s/(.*)(\$.).9(.*)(.)/\1\2\40\3/
ba}
s/(.*)(\$..).(.*)(.)/\1\2\4\3/
ba}
/%-/{
/\$000/{
s/\$000/$255/
ba}
/\$.00/{
s/\$200/$199/
s/\$100/$099/
ba}
/\$..0/s/.*\$.(.)0.*/&\1/
/\$..[1-9]/s/.*\$..(.).*/&\1/
s/1$/0/
s/2$/1/
s/3$/2/
s/4$/3/
s/5$/4/
s/6$/5/
s/7$/6/
s/8$/7/
s/9$/8/
/\$..0/{
s/(.*)(\$.).0(.*)(.)/\1\2\49\3/
ba}
s/(.*)(\$..).(.*)(.)/\1\2\4\3/
ba}
/%>/{
s/\$(...);/$\1000;/
s/\$(...)(...)/\1$\2/
ba}
/%</{
/#\$/s/\$/000$/
s/([0-9]{3})\$(...)/$\1\2/
ba}
/%,/{
/![0-9]{3}/{
s/(.*)!(...)(.*)\$...(.*)/\1!\3\$\2\4/
ba}
s/\$.../\$000/
ba}
/%\./{
s/.*\$(...).*/&\1 /
ba}
/%\[/{
/\$(..[1-9]|.[1-9].|[1-9]..)/ba
s/$/a/
s/%./&@/
:b
/@\]/s/a$//
/@\[/s/$/a/
/a$/s/@(.)/\1@/
tb
s/%//
s/@/%/
ba}
/%\]/{
s/$/a/
s/.%/@&/
:c
/@\[/s/a$//
/@\]/s/$/a/
/a$/s/(.)@/@\1/
tc
s/%//
s/(.)@/%\1/
ba}
s/.*;//
