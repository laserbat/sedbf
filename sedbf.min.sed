#!/bin/sed -Ef
s/[^][+,.<>!0-9-]//g
s/^/% /
s/$/#$000;/
:a
s/%(.)/\1%/
/%\+/{/\$255/{s/\$255/$000/
ba}
/\$.99/{s/\$099/$100/
s/\$199/$200/
ba}
/\$..9/s/(\$..)./\1_0/
/\$..[0-8]/s/\$.../&_/
s/8_/9/
s/7_/8/
s/6_/7/
s/5_/6/
s/4_/5/
s/3_/4/
s/2_/3/
s/1_/2/
s/0_/1/
ba}
/%-/{/\$000/{s/\$000/$255/
ba}
/\$.00/{s/\$200/$199/
s/\$100/$099/
ba}
/\$..0/s/(\$..)./\1_9/
/\$..[1-9]/s/\$.../&_/
s/1_/0/
s/2_/1/
s/3_/2/
s/4_/3/
s/5_/4/
s/6_/5/
s/7_/6/
s/8_/7/
s/9_/8/
ba}
/%>/{s/\$(...);/$\1000;/
s/\$(...)(...)/\1$\2/
ba}
/%</{/#\$/s/\$/000$/
s/([0-9]{3})\$(...)/$\1\2/
ba}
/%,/{/![0-9]{3}/{s/!(...)(.*)\$.../!\2\$\1/
ba}
s/\$.../\$000/
ba}
/%\./{s/.*\$(...).*/&\1 /
ba}
/%\[/{/\$(..[1-9]|.[1-9].|[1-9]..)/ba
s/$/a/
s/%./&@/
:b
/@\]/s/a$//
/@\[/s/$/a/
/a$/s/@(.)/\1@/
tb
s/%//
s/@/%/
ba}
/%\]/{s/$/a/
s/.%/@&/
:c
/@\[/s/a$//
/@\]/s/$/a/
/a$/s/(.)@/@\1/
tc
s/%//
s/(.)@/%\1/
ba}
s/.*;//
